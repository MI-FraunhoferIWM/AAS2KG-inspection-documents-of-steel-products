PREFIX aas: <https://admin-shell.io/aas/3/0/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX ns1: <https://admin-shell.io/aas/3/0/Property/>
PREFIX ns2: <https://admin-shell.io/aas/3/0/Key/>
PREFIX ns3: <https://admin-shell.io/aas/3/0/Reference/>
PREFIX ns5: <https://admin-shell.io/aas/3/0/HasSemantics/>

PREFIX ex: <http://www.example.org/#>
PREFIX material: <https://w3id.org/pmd/co/PMD_0000000>
PREFIX chemical_composition: <https://w3id.org/pmd/co/PMD_0000551>
PREFIX chemical_composition_specification: <https://w3id.org/pmd/co/PMD_0025002>
PREFIX fraction_value_specification: <https://w3id.org/pmd/co/PMD_0025997>
PREFIX mass_proportion: <https://w3id.org/pmd/co/PMD_0020102>
PREFIX portion_of_silicon: <https://w3id.org/pmd/co/PMD_0020050>
PREFIX portion_of_carbon: <https://w3id.org/pmd/co/PMD_0020049>

PREFIX quality_of: <http://purl.obolibrary.org/obo/RO_0000080>
PREFIX has_member: <http://purl.obolibrary.org/obo/RO_0002351>
PREFIX is_subject_of: <https://w3id.org/pmd/co/PMD_0000004>
PREFIX specified_by_value: <https://w3id.org/pmd/co/PMD_0000077>
PREFIX relational_quality_of: <https://w3id.org/pmd/co/PMD_0025999>
PREFIX has_specified_numeric_value: <http://purl.obolibrary.org/obo/OBI_0001937>
PREFIX has_measurement_unit_label: <http://purl.obolibrary.org/obo/IAO_0000039>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>

PREFIX mass_percentage: <http://purl.obolibrary.org/obo/UO_0000163>

CONSTRUCT {

  ex:PLACEHOLDER_material a material: .

  ex:PLACEHOLDER_some_carbon a portion_of_carbon: ;
      part_of: ex:PLACEHOLDER_material .

  ex:PLACEHOLDER_some_silicon a portion_of_silicon: ;
      part_of: ex:PLACEHOLDER_material .

  ex:PLACEHOLDER_chem_comp a chemical_composition: ;
      quality_of: ex:PLACEHOLDER_material ;
      is_subject_of: ex:PLACEHOLDER_chem_comp_spec .

  ex:PLACEHOLDER_chem_comp_spec a chemical_composition_specification: ;
      has_member: ex:PLACEHOLDER_fraction_carbon ;
      has_member: ex:PLACEHOLDER_fraction_silicon .

  ex:PLACEHOLDER_mass_proportion_carbon a mass_proportion: ;
      relational_quality_of: ex:PLACEHOLDER_some_carbon ;
      specified_by_value: ex:PLACEHOLDER_fraction_carbon .

  ex:PLACEHOLDER_mass_proportion_silicon a mass_proportion: ;
      relational_quality_of: ex:PLACEHOLDER_some_silicon ;
      specified_by_value: ex:PLACEHOLDER_fraction_silicon .

  ex:PLACEHOLDER_fraction_carbon a fraction_value_specification: ;
      has_specified_numeric_value: ?carbonValueStr ;
      has_measurement_unit_label: mass_percentage: .

  ex:PLACEHOLDER_fraction_silicon a fraction_value_specification: ;
      has_specified_numeric_value: ?siliconValueStr ;
      has_measurement_unit_label: mass_percentage: .

}

WHERE {

    # Carbon property
  ?carbonProp a aas:Property ;
      ns1:value ?carbonValue ;
      ns5:supplementalSemanticIds ?carbonSS .

  ?carbonSS ns3:keys ?carbonKey .
  ?carbonKey ns2:value "http://purl.obolibrary.org/obo/CHEBI_27573" .

  # Silicon property
  ?siliconProp a aas:Property ;
      ns1:value ?siliconValue ;
      ns5:supplementalSemanticIds ?siliconSS .

  ?siliconSS ns3:keys ?siliconKey .
  ?siliconKey ns2:value "http://purl.obolibrary.org/obo/CHEBI_27594" .



  # Convert to string literals (optional, if needed)
  BIND(STR(?carbonValue) AS ?carbonValueStr)
  BIND(STR(?siliconValue) AS ?siliconValueStr)
}
