PREFIX aas: <https://admin-shell.io/aas/3/0/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX ns1: <https://admin-shell.io/aas/3/0/Property/>
PREFIX ns2: <https://admin-shell.io/aas/3/0/Key/>
PREFIX ns3: <https://admin-shell.io/aas/3/0/Reference/>
PREFIX ns5: <https://admin-shell.io/aas/3/0/HasSemantics/>

PREFIX ex: <http://www.example.org/#>
PREFIX material: <https://w3id.org/pmd/co/PMD_0000000>
PREFIX chemical_composition: <https://w3id.org/pmd/co/PMD_0000551>
PREFIX chemical_composition_specification: <https://w3id.org/pmd/co/PMD_0025002>
PREFIX fraction_value_specification: <https://w3id.org/pmd/co/PMD_0025997>
PREFIX mass_proportion: <https://w3id.org/pmd/co/PMD_0020102>
PREFIX portion_of_nickel: <https://w3id.org/pmd/co/PMD_0020051>
PREFIX portion_of_carbon: <https://w3id.org/pmd/co/PMD_0020030>
PREFIX portion_of_chromium: <https://w3id.org/pmd/co/PMD_0020029>

PREFIX quality_of: <http://purl.obolibrary.org/obo/RO_0000080>
PREFIX has_member: <http://purl.obolibrary.org/obo/RO_0002351>
PREFIX is_subject_of: <https://w3id.org/pmd/co/PMD_0000004>
PREFIX specified_by_value: <https://w3id.org/pmd/co/PMD_0000077>
PREFIX relational_quality_of: <https://w3id.org/pmd/co/PMD_0025999>
PREFIX has_specified_numeric_value: <http://purl.obolibrary.org/obo/OBI_0001937>
PREFIX has_measurement_unit_label: <http://purl.obolibrary.org/obo/IAO_0000039>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>

PREFIX mass_percentage: <http://purl.obolibrary.org/obo/UO_0000163>

CONSTRUCT {

  ex:PLACEHOLDER_material a material: .

  ex:PLACEHOLDER_some_carbon a portion_of_carbon: ;
      part_of: ex:PLACEHOLDER_material .

  ex:PLACEHOLDER_some_nickel a portion_of_nickel: ;
      part_of: ex:PLACEHOLDER_material .

  ex:PLACEHOLDER_some_chromium a portion_of_chromium: ;
      part_of: ex:PLACEHOLDER_material .

  ex:PLACEHOLDER_chem_comp a chemical_composition: ;
      quality_of: ex:PLACEHOLDER_material ;
      is_subject_of: ex:PLACEHOLDER_chem_comp_spec .

  ex:PLACEHOLDER_chem_comp_spec a chemical_composition_specification: ;
      has_member: ex:PLACEHOLDER_fraction_carbon ;
      has_member: ex:PLACEHOLDER_fraction_chromium ;
      has_member: ex:PLACEHOLDER_fraction_nickel .

  ex:PLACEHOLDER_mass_proportion_carbon a mass_proportion: ;
      relational_quality_of: ex:PLACEHOLDER_some_carbon ;
      specified_by_value: ex:PLACEHOLDER_fraction_carbon .

  ex:PLACEHOLDER_mass_proportion_nickel a mass_proportion: ;
      relational_quality_of: ex:PLACEHOLDER_some_nickel ;
      specified_by_value: ex:PLACEHOLDER_fraction_nickel .

  ex:PLACEHOLDER_mass_proportion_chromium a mass_proportion: ;
      relational_quality_of: ex:PLACEHOLDER_some_chromium ;
      specified_by_value: ex:PLACEHOLDER_fraction_chromium .

  ex:PLACEHOLDER_fraction_carbon a fraction_value_specification: ;
      has_specified_numeric_value: ?carbonValueStr ;
      has_measurement_unit_label: mass_percentage: .

  ex:PLACEHOLDER_fraction_nickel a fraction_value_specification: ;
      has_specified_numeric_value: ?nickelValueStr ;
      has_measurement_unit_label: mass_percentage: .

  ex:PLACEHOLDER_fraction_chromium a fraction_value_specification: ;
      has_specified_numeric_value: ?chromiumValueStr ;
      has_measurement_unit_label: mass_percentage: .

}

WHERE {

    # Carbon property
  ?carbonProp a aas:Property ;
      ns1:value ?carbonValue ;
      ns5:supplementalSemanticIds ?carbonSS .

  ?carbonSS ns3:keys ?carbonKey .
  ?carbonKey ns2:value "http://purl.obolibrary.org/obo/CHEBI_27594" .

  # Nickel property
  ?nickelProp a aas:Property ;
      ns1:value ?nickelValue ;
      ns5:supplementalSemanticIds ?nickelSS .

  ?nickelSS ns3:keys ?nickelKey .
  ?nickelKey ns2:value "http://purl.obolibrary.org/obo/CHEBI_28112" .

  # Chromium property
  ?chromiumProp a aas:Property ;
      ns1:value ?chromiumValue ;
      ns5:supplementalSemanticIds ?chromiumSS .

  ?chromiumSS ns3:keys ?chromiumKey .
  ?chromiumKey ns2:value "http://purl.obolibrary.org/obo/CHEBI_28073" .



  # Convert to string literals (optional, if needed)
  BIND(STR(?carbonValue) AS ?carbonValueStr)
  BIND(STR(?nickelValue) AS ?nickelValueStr)
  BIND(STR(?chromiumValue) AS ?chromiumValueStr)
}
